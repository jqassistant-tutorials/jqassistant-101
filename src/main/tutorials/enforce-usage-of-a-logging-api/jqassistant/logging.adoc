// tag::loggingDefault[]
[[logging:Default]]
[role=group,includesConstraints="logging:Slf4jLoggerMustBeUsed"]
== Logging

This document describes the rules regarding logging.
// end::loggingDefault[]

// tag::loggingLogger[]
[[logging:Logger]]
[source,cypher,role=concept]
.Labels the Logger APIs of known logging frameworks with `Logger`.
----
MATCH
  (logger:Type)
WHERE
  logger.fqn in [
    "java.util.logging.Logger",
    "org.apache.commons.logging.Log",
    "org.apache.log4j.Logger",
    "org.apache.logging.log4j.Logger",
    "org.slf4j.Logger"
  ]
SET
  logger:Logger
RETURN
  logger as Logger
----
// end::loggingLogger[]

// tag::loggingSlf4jLogger[]
[[logging:Slf4jLogger]]
[source,cypher,role=concept,requiresConcepts="logging:Logger"]
.Adds the label `Slf4j` to the type `org.slf4j.Logger`.
----
MATCH
  (slf4jLogger:Logger)
WHERE
  slf4jLogger.fqn = "org.slf4j.Logger"
SET
  slf4jLogger:Slf4j
RETURN
  slf4jLogger
----
// end::loggingSlf4jLogger[]

// tag::loggingSlf4jLoggerMustBeUsed[]
[[logging:Slf4jLoggerMustBeUsed]]
[source,cypher,role=constraint,requiresConcepts="logging:Logger,logging:Slf4jLogger"]
.Only methods provided by the Slf4j API must be used for logging.
----
MATCH
  (:Type)-[:DECLARES]-(method:Method)-[invokes:INVOKES]->(loggerMethod:Method),
  (logger:Logger)-[:DECLARES]->(loggerMethod)
WHERE NOT
  logger:Slf4j
RETURN
  method as Method, invokes.lineNumber as LineNumber, logger as InvalidLogger
----
// end::loggingSl4fjLoggerMustBeUsed[]

