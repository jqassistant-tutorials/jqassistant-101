// tag::moduleDefault[]
[[module:Default]]
[role=group,includesConcepts="module:*"]
== Module

This section describes rules regarding modules and their dependencies.
// end::moduleDefault[]

// tag::moduleModule[]
[[module:Module]]
[source,cypher,role=concept]
.All packages in the root package of the main artifact are labeled as `Module`.
----
MATCH
  (:Main:Artifact)-[:CONTAINS]->(root:Package)-[:CONTAINS]->(module:Package)
WHERE
  root.fqn = "your.project"
SET
  module:Module
RETURN
  module.name as Module, module.fqn as Package
ORDER BY
  module.name
----
// end::moduleModule[]

// tag::moduleDependencies[]
[[module:Dependencies]]
[source,cypher,role=concept,requiresConcepts="module:Module",reportType="plantuml-component-diagram"]
.A module depends on another module if there is a dependency of Java types between both.
----
MATCH
  (module1:Module)-[:CONTAINS*]->(t1:Type),
  (module2:Module)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE
  module1 <> module2
WITH
  module1, module2, COUNT(*) AS weight
MERGE
  (module1)-[dependsOn:DEPENDS_ON_MODULE]->(module2)
SET
  dependsOn.weight = weight
RETURN
  module1, dependsOn, module2
----
// end::moduleDependencies[]

// tag::moduleDependenciesGraphML[]
[[module:DependenciesGraphML]]
[source,cypher,role=concept,requiresConcepts="module:Dependencies",reportType="graphml"]
.Modules and their dependencies as GraphML report.
----
MATCH
  (module:Module)
OPTIONAL MATCH
  (module)-[dependsOn:DEPENDS_ON_MODULE]->(:Module)
RETURN
  *
----
// end::moduleDependenciesGraphML[]

// tag::moduleDependenciesGraphMLDrilldown[]
[[module:DependenciesGraphMLDrilldown]]
[source,cypher,role=concept,requiresConcepts="module:Module",reportType="graphml"]
.Modules and their dependencies as GraphML report with drill-down to type level.
----
MATCH
  (module:Module)-[:CONTAINS*]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)<-[:CONTAINS*]-(:Module)
RETURN
  {
    role: "graph",
    parent: module,
    nodes: collect(type),
    relationships: collect(dependsOn)
  }
----
// end::moduleDependenciesGraphMLDrilldown[]

// tag::moduleClassDiagram[]
[[module:ClassDiagram]]
[source,cypher,role=concept,requiresConcepts="module:Module",reportType="plantuml-class-diagram"]
.Class Diagram
----
MATCH
  (module:Module:Package)-[:CONTAINS*]->(type:Type)
OPTIONAL MATCH
  (type)-[extends:EXTENDS|IMPLEMENTS]->(:Type)
OPTIONAL MATCH
  (type)-[:DECLARES]->(field:Field)
OPTIONAL MATCH
  (type)-[:DECLARES]->(method:Method)
RETURN
  *
----
// end::moduleClassDiagram[]

// tag::moduleSequenceDiagram[]
[[module:SequenceDiagram]]
[source,cypher,role=concept,reportType="plantuml-sequence-diagram"]
.Sequence Diagram
----
MATCH
  (:Type{name:"ServiceCImpl"})-[:DECLARES]->(method:Method{name:"run"}),
  sequence=((method)-[:INVOKES*]->(:Method))
RETURN
  sequence
----
// end::moduleSequenceDiagram[]

